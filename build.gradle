buildscript {
  ext {
    springBootVersion = '2.0.1.RELEASE'
  }
  repositories {
    mavenLocal()
    jcenter()
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.4'
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'
    classpath 'ru.alfalab.gradle.platform.stack:platform-gradle-stacks-plugin:0.3.13'
  }
}

ext {
  grpcVersion = '1.9.1'
  protobufVersion = '3.5.1'
  exampleProjects = allprojects.findAll { it.name.startsWith 'example-' }
}

group = 'ru.alfalab.grpc.spring'

apply plugin: 'com.jfrog.bintray'

allprojects {
  repositories {
    maven {
      url 'https://repo.spring.io/libs-milestone'
    }
    jcenter()
  }

  apply plugin: 'idea'
  apply plugin: 'nebula.info'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'nebula.release'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'jacoco'
  apply plugin: 'nebula.optional-base'

  group = rootProject.group

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  dependencyManagement {
    imports {
      mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
      mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Finchley.M9'
    }
    dependencies {
      dependencySet(group: 'io.grpc', version: grpcVersion) {
        entry 'grpc-all'
        entry 'grpc-netty'
        entry 'grpc-protobuf'
        entry 'grpc-stub'
      }

      dependency "com.google.protobuf:protobuf-java:$protobufVersion"
      dependency 'org.projectlombok:lombok:1.16.14'
      dependency 'org.codehaus.groovy:groovy-all:2.4.8'

      dependency 'org.spockframework:spock-core:1.1-groovy-2.4-rc-3'
      dependency 'org.spockframework:spock-spring:1.1-groovy-2.4-rc-3'
    }
  }

  dependencies {
    compileOnly 'org.projectlombok:lombok'

    testCompile 'org.codehaus.groovy:groovy-all'
    testCompile 'org.spockframework:spock-core'
    testCompile 'org.spockframework:spock-spring'
  }

  test {
    reports {
      junitXml.enabled = true
      html.enabled = true
    }
  }

  jacocoTestReport {
    reports {
      xml.enabled true
      csv.enabled true
      html.destination "${buildDir}/jacocoHtml"
    }
  }
}

task testReport(type: TestReport) {
  destinationDir = file("$buildDir/reports/allTests")
  reportOn subprojects*.test
}

configure(subprojects - exampleProjects - project(':examples')) {
  apply plugin: 'com.github.kt3k.coveralls'
  apply plugin: 'nebula.maven-publish'
  apply plugin: 'com.jfrog.bintray'
  apply plugin: 'nebula.source-jar'
  apply plugin: 'nebula.javadoc-jar'

  bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['nebula']
    dryRun = false
    publish = true
    pkg {
      repo = 'maven'
      name = 'grpc-spring-boot-starter'
      userOrg = 'lavcraft'
      licenses = ['MIT']
      websiteUrl = 'https://github.com/lavcraft/grpc-spring-boot-starter'
      vcsUrl = 'https://github.com/lavcraft/grpc-spring-boot-starter'
      labels = ['grpc', 'java', 'spring', 'spring-boot', 'spring-boot-starter']
      publicDownloadNumbers = true
      attributes = [:]
      version {
        name = project.version
        vcsTag = project.version
        attributes = [:]
      }
    }
  }

  bintrayUpload.onlyIf { !project.version.toString().toUpperCase().endsWith("-SNAPSHOT") }
}

/**
 * !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
 * Configuration for examples
 * Copy and use, and get plugins/buildscript block from above
 * !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
 */
configure(exampleProjects) {
  apply plugin: 'java'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'com.google.protobuf'

  ext {
    protoGenDir = file('generated-src/')
  }

  dependencies {
    compile 'io.grpc:grpc-stub'
    compile 'io.grpc:grpc-protobuf'
    compile 'io.grpc:grpc-netty'
    compile 'io.netty:netty-common'
    compile 'io.netty:netty-transport'
    compile 'org.springframework.boot:spring-boot-starter-actuator'

    compile project(':starter')

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.cloud:spring-cloud-commons'
  }

  sourceSets {
    main {
      proto {
        srcDir 'src/main/proto'
      }
      java {
        srcDir "$protoGenDir/main/java"
      }
    }
  }

  idea {
    module {
      sourceDirs += file('./src/main/proto')
      sourceDirs += file("${protoGenDir}")
    }
  }

  protobuf {
    protoc {
      artifact = "com.google.protobuf:protoc:$protobufVersion"
    }

    plugins {
      grpc {
        artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
      }
    }

    generateProtoTasks {
      ofSourceSet('main').each { task ->
        task.builtins {
          java {
            outputSubDir = 'java'
          }
        }
        task.plugins {
          grpc {
            outputSubDir = 'java'
          }
        }
      }
    }
    generatedFilesBaseDir = protoGenDir
  }

  task cleanProtoGen {
    doFirst {
      delete "${protoGenDir}"
    }
  }
  clean.dependsOn cleanProtoGen
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.5.1'
}
